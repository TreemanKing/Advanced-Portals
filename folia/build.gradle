

plugins {
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}

configurations {
    // configuration that holds jars to copy into lib
    includeLibs
}
archivesBaseName = "Advanced-Portals-Folia"

repositories {
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
    implementation project(":core")
    runtimeOnly project(path: ':core', configuration: 'shadow')

    compileOnly 'dev.folia:folia-api:1.19.4-R0.1-SNAPSHOT'

    includeLibs project(':lang')
    includeLibs project(':core')
    includeLibs project(':bungee')
    includeLibs project(':velocity')
}

tasks.named('compileJava') {
    dependsOn(':core:shadowJar')
}

jar {
    dependsOn(':bungee:jar', ':lang:jar', ':proxycore:jar', ':velocity:jar')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Filters the files out that are in the build folders. Look to see if there is a better way to do this?
    from configurations.includeLibs.filter {
        it.path.contains("${File.separator}build${File.separator}libs")
    } .collect {
        println("Will Include: ${it.name}")
        it.isDirectory() ? it : zipTree(it)
    }
}